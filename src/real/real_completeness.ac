from nat import ℕ
from rat import ℚ
from real.real_base import ℝ, is_cut, is_lower_interval, is_greatest, has_greatest, is_dedekind_cut
numerals ℝ
from set import Set, empty, intersection, constant_false, fn_image

attributes ℝ {
    /// True if this real number is an upper bound for the set s.
    define is_set_upper_bound(self, s: Set[ℝ]) -> Bool {
        forall(y: ℝ) {
            y ∈ s implies y <= self
        }
    }

    /// True if this real number is a lower bound for the set s.
    define is_set_lower_bound(self, s: Set[ℝ]) -> Bool {
        forall(y: ℝ) {
            y ∈ s implies self <= y
        }
    }

    /// True if this real number is the least upper bound (supremum) for the set s.
    define is_set_least_upper_bound(self, s: Set[ℝ]) -> Bool {
        self.is_set_upper_bound(s) and
        forall(y: ℝ) {
            y.is_set_upper_bound(s) implies self <= y
        }
    }

    /// True if this real number is the greatest lower bound (infimum) for the set s.
    define is_set_greatest_lower_bound(self, s: Set[ℝ]) -> Bool {
        self.is_set_lower_bound(s) and
        forall(y: ℝ) {
            y.is_set_lower_bound(s) implies y <= self
        }
    }
}

/// The least upper bound of a set is unique.
theorem uniqueness_of_least_upper_bound(u1: ℝ, u2: ℝ, s: Set[ℝ]) {
    u1.is_set_least_upper_bound(s) and u2.is_set_least_upper_bound(s) implies u1 = u2
}

/// The greatest lower bound of a set is unique.
theorem uniqueness_of_greatest_lower_bound(u1: ℝ, u2: ℝ, s: Set[ℝ]) {
    u1.is_set_greatest_lower_bound(s) and u2.is_set_greatest_lower_bound(s) implies u1 = u2
}

/// True if s1 and s2 have the same set of upper bounds.
define same_upper_bound(s1: Set[ℝ], s2: Set[ℝ]) -> Bool {
    forall(r: ℝ) {
        r.is_set_upper_bound(s1) iff r.is_set_upper_bound(s2)
    }
}

/// True if s1 and s2 have the same least upper bounds (suprema).
define same_least_upper_bound(s1: Set[ℝ], s2: Set[ℝ]) -> Bool {
    forall(r: ℝ) {
        r.is_set_least_upper_bound(s1) iff r.is_set_least_upper_bound(s2)
    }
}

/// If two sets have the same upper bounds, then a least upper bound for one set
/// is also a least upper bound for the other set.
theorem same_upper_bound_imp_same_least_upper_bound_lemma(s1: Set[ℝ], s2: Set[ℝ], r: ℝ) {
    same_upper_bound(s1, s2) and r.is_set_least_upper_bound(s1) implies r.is_set_least_upper_bound(s2)
} by {
    forall(x: ℝ) {
        if x.is_set_upper_bound(s2) {
            x.is_set_upper_bound(s1)
            r <= x
        }
    }
    r.is_set_least_upper_bound(s2)
}

/// If two sets have the same upper bounds, then they have the same least upper bounds.
theorem same_upper_bound_imp_same_least_upper_bound(s1: Set[ℝ], s2: Set[ℝ]) {
    same_upper_bound(s1, s2) implies same_least_upper_bound(s1, s2)
} by {
    forall(r: ℝ) {
        r.is_set_least_upper_bound(s1) implies r.is_set_least_upper_bound(s2)
        r.is_set_least_upper_bound(s2) implies r.is_set_least_upper_bound(s1)
        r.is_set_least_upper_bound(s1) iff r.is_set_least_upper_bound(s2)
    }
}

/// True if s1 and s2 have the same set of lower bounds.
define same_lower_bound(s1: Set[ℝ], s2: Set[ℝ]) -> Bool {
    forall(r: ℝ) {
        r.is_set_lower_bound(s1) iff r.is_set_lower_bound(s2)
    }
}

/// True if s1 and s2 have the same greatest lower bounds (infima).
define same_greatest_lower_bound(s1: Set[ℝ], s2: Set[ℝ]) -> Bool {
    forall(r: ℝ) {
        r.is_set_greatest_lower_bound(s1) iff r.is_set_greatest_lower_bound(s2)
    }
}

/// If two sets have the same lower bounds, then a greatest lower bound for one set
/// is also a greatest lower bound for the other set.
theorem same_lower_bound_imp_same_greatest_lower_bound_lemma(s1: Set[ℝ], s2: Set[ℝ], r: ℝ) {
    same_lower_bound(s1, s2) and r.is_set_greatest_lower_bound(s1) implies r.is_set_greatest_lower_bound(s2)
} by {
    forall(x: ℝ) {
        if x.is_set_lower_bound(s2) {
            x.is_set_lower_bound(s1)
            r >= x
        }
    }
    r.is_set_greatest_lower_bound(s2)
}

/// If two sets have the same lower bounds, then they have the same greatest lower bounds.
theorem same_lower_bound_imp_same_least_lower_bound(s1: Set[ℝ], s2: Set[ℝ]) {
    same_lower_bound(s1, s2) implies same_greatest_lower_bound(s1, s2)
} by {
    forall(r: ℝ) {
        r.is_set_greatest_lower_bound(s1) implies r.is_set_greatest_lower_bound(s2)
        r.is_set_greatest_lower_bound(s2) implies r.is_set_greatest_lower_bound(s1)
        r.is_set_greatest_lower_bound(s1) iff r.is_set_greatest_lower_bound(s2)
    }
}

/// Any lower bound of a nonempty set is less than or equal to any upper bound of the set.
theorem lower_lte_upper_bound(s: Set[ℝ], l: ℝ, u: ℝ) {
    (s).is_nonempty and l.is_set_lower_bound(s) and u.is_set_upper_bound(s) implies l <= u
} by {
    let x: ℝ satisfy { x ∈ s }
    l <= x
    x <= u
    l <= u
}

/// The greatest lower bound of a nonempty set is less than or equal to its least upper bound.
theorem greatest_lower_lte_least_upper_bound(s: Set[ℝ], l: ℝ, u: ℝ) {
    (s).is_nonempty and l.is_set_greatest_lower_bound(s) and u.is_set_least_upper_bound(s) implies l <= u
}

// The supremum condition on s is that it's nonempty, and has
// an upper bound.
// sup_cut is the Dedekind cut that will be the supremum of s.
// First we define it, then we prove that it is a Dedekind cut.
/// All r ∈ ℚ that r < sup(s)  
/// Or written as ∪(a ∈ s, ℚ_(< a)) = ℚ_(< sup(s))
define sup_cut(s: Set[ℝ], r: ℚ) -> Bool {
    not ℝ.from_rat(r).is_set_upper_bound(s)
}

/// If s is nonempty and has an upper bound, then sup_cut(s) is a cut (nonempty and has a complement).
theorem sup_cut_is_cut(s: Set[ℝ], ub: ℝ) {
    (s).is_nonempty and ub.is_set_upper_bound(s)
    implies
    is_cut(sup_cut(s))
} by {
    let x: ℝ satisfy {
        x ∈ s
    }

    let r1: ℚ satisfy {
        ℝ.from_rat(r1) < x
    }

    not ℝ.from_rat(r1).is_set_upper_bound(s)
    sup_cut(s, r1)

    let r2: ℚ satisfy {
        ℝ.from_rat(r2) > ub
    }

    // Show that r2 is an upper bound of s
    forall(y: ℝ) {
        if y ∈ s {
            y <= ub
            ub < ℝ.from_rat(r2)
            y < ℝ.from_rat(r2)
            y <= ℝ.from_rat(r2)
        }
    }
    ℝ.from_rat(r2).is_set_upper_bound(s)
    not sup_cut(s, r2)
}

/// If s is nonempty and has an upper bound, then sup_cut(s) is a lower interval
/// (downward closed: if q is in the cut and p < q, then p is in the cut).
theorem sup_cut_is_lower_interval(s: Set[ℝ], ub: ℝ) {
    (s).is_nonempty and ub.is_set_upper_bound(s)
    implies
    is_lower_interval(sup_cut(s))
} by {
    forall(x: ℚ, y: ℚ) {
        if sup_cut(s, y) and x < y {
            not ℝ.from_rat(y).is_set_upper_bound(s)
            let z: ℝ satisfy {
                z ∈ s and z > ℝ.from_rat(y)
            }
            z > ℝ.from_rat(x)
            not ℝ.from_rat(x).is_set_upper_bound(s)
            sup_cut(s, x)
        }
    }
}

// r ∈ ℚ, r < sup(s) have no greatest
theorem sup_cut_not_has_greatest(s: Set[ℝ], ub: ℝ) {
    (s).is_nonempty and ub.is_set_upper_bound(s)
    implies
    not has_greatest(sup_cut(s))
} by {
    if has_greatest(sup_cut(s)) {
        let q: ℚ satisfy {
            is_greatest(sup_cut(s), q)
        }

        sup_cut(s, q)
        not ℝ.from_rat(q).is_set_upper_bound(s)
        let z: ℝ satisfy {
            z ∈ s and z > ℝ.from_rat(q)
        }

        ℝ.from_rat(q) < z
        let q_prime: ℚ satisfy {
            q < q_prime and ℝ.from_rat(q_prime) < z
        }

        // Since ℝ.from_rat(q_prime) < z and z is in s,
        // ℝ.from_rat(q_prime) is not an upper bound for s
        not ℝ.from_rat(q_prime).is_set_upper_bound(s)

        // Therefore q_prime is in the cut
        sup_cut(s, q_prime)

        // But q < q_prime, contradicting our assumption
        not q_prime <= q
        not is_greatest(sup_cut(s), q)
        false
    }
}

define have_upper_bound(s: Set[ℝ]) -> Bool {
    exists(ub: ℝ) {
        ub.is_set_upper_bound(s)
    }
}

/// If s is nonempty and has an upper bound, then sup_cut(s) is a Dedekind cut.
theorem sup_cut_is_dedekind_cut(s: Set[ℝ], ub: ℝ) {
    (s).is_nonempty and have_upper_bound(s)
    implies
    is_dedekind_cut(sup_cut(s))
}

/// If sup_cut(s) is a Dedekind cut, then s is nonempty and has an upper bound.
theorem sup_dedekind_cut_imp_no_empty_and_have_upper_bound(s: Set[ℝ]) {
    is_dedekind_cut(sup_cut(s)) implies (s).is_nonempty and have_upper_bound(s)
} by {
    is_cut(sup_cut(s))
}

/// The supremum (least upper bound) of s, if it exists.
/// Returns 0 if s is empty or has no upper bound.
let sup = function(s: Set[ℝ]) {
    if (s).is_nonempty and have_upper_bound(s) {
        ℝ.new(sup_cut(s))
    } else {
        0
    }
}

/// True if s is nonempty and has an upper bound (so sup(s) exists).
define have_sup(s: Set[ℝ]) -> Bool {
    (s).is_nonempty and have_upper_bound(s)
}

/// If s is nonempty and has an upper bound, then sup(s) is its least upper bound.
/// This proves the existence of the supremum for bounded nonempty sets.
theorem ub_imp_lub(s: Set[ℝ], x: ℝ) {
    have_sup(s)
    implies sup(s).is_set_least_upper_bound(s)
} by {
    (s).is_nonempty
    have_upper_bound(s)
    is_dedekind_cut(sup_cut(s))
    let y = sup(s)
    y = ℝ.new(sup_cut(s))

    // Part 1: Show y is an upper bound of s
    forall(z: ℝ) {
        if z ∈ s {
            if z > y {
                let q: ℚ satisfy {
                    z > ℝ.from_rat(q) and ℝ.from_rat(q) > y
                }
                not y.gt_rat(q)
                not sup_cut(s, q)
                ℝ.from_rat(q).is_set_upper_bound(s)
                z <= ℝ.from_rat(q)
                false
            }
        }
    }
    y.is_set_upper_bound(s)

    // Part 2: Show y is the least upper bound
    forall(w: ℝ) {
        if w.is_set_upper_bound(s) {
            // We'll prove y <= w by contradiction
            if y > w {
                let q: ℚ satisfy {
                    y > ℝ.from_rat(q) and ℝ.from_rat(q) > w
                }
                not y <= ℝ.from_rat(q)
                sup_cut(s, q)
                not ℝ.from_rat(q).is_set_upper_bound(s)
                let z: ℝ satisfy {
                    z ∈ s and z > ℝ.from_rat(q)
                }
                z <= w
                ℝ.from_rat(q) > w
                z > ℝ.from_rat(q)
                z > w
                false
            }
        }
    }
}

/// Any upper bound of a nonempty bounded set is greater than or equal to its supremum.
theorem upper_bound_lte_sup(s: Set[ℝ], u: ℝ) {
    (s).is_nonempty and u.is_set_upper_bound(s) implies sup(s) <= u
}

/// Any element of s is less than or equal to its least upper bound.
theorem lte_sup(s: Set[ℝ], y: ℝ, a: ℝ) {
    y.is_set_least_upper_bound(s) and a ∈ s implies a <= y
}

/// For any ε > 0, there exists an element of s within ε of the least upper bound y,
/// and this element is greater than y - ε.
theorem closely_lt_sup(s: Set[ℝ], y: ℝ, ε: ℝ) {
    y.is_set_least_upper_bound(s) and ε > 0 implies exists(a: ℝ) {
        a ∈ s and y - ε < a and a <= y
    } 
} by {
    y - ε < y

    not (y - ε).is_set_upper_bound(s)

    let a: ℝ satisfy {
        a ∈ s and not a <= y - ε
    }

    a > y - ε

    a <= y

    a ∈ s and y - ε < a and a <= y
}

/// For any ε > 0, there exists an element of s within ε of sup(s),
/// and this element is greater than sup(s) - ε.
theorem closely_lt_sup_directly(s: Set[ℝ], ε: ℝ) {
    have_sup(s) and ε > 0 implies exists(a: ℝ) {
        a ∈ s and sup(s) - ε < a and a <= sup(s)
    } 
} by {
    sup(s).is_set_least_upper_bound(s)
}

/// If l is less than the least upper bound y of s, then there exists an element of s
/// between l and y.
theorem directly_closely_lt_sup(s: Set[ℝ], y: ℝ, l: ℝ) {
    y.is_set_least_upper_bound(s) and l < y implies exists(a: ℝ) {
        a ∈ s and l < a and a <= y
    }
} by {
    let ε = y - l
    ε > 0
    y - ε = y + -(y - l)
    y - ε = l
}

/// If l is less than sup(s), then there exists an element of s between l and sup(s).
theorem directly_closely_lt_sup_directly(s: Set[ℝ], l: ℝ) {
    have_sup(s) and l < sup(s) implies exists(a: ℝ) {
        a ∈ s and l < a and a <= sup(s)
    }
} 

/// Transform a predicate f to its negated form.
define flip_fn(f: ℝ -> Bool, x: ℝ) -> Bool {
    f(-x)
}

/// Flipping a predicate twice returns the original predicate.
theorem flip_flip_fn(f: ℝ -> Bool) {
    flip_fn(flip_fn(f)) = f
} by {
    forall(x: ℝ) {
        flip_fn(flip_fn(f), x) = flip_fn(f)(-x)
        flip_fn(flip_fn(f), x) = f(x)
    }
}

/// The set obtained by negating all elements of s.
define flip(s: Set[ℝ]) -> Set[ℝ] {
    Set[ℝ].new(flip_fn(s.contains))
}

/// Negating all elements twice returns the original set.
theorem flip_flip(s: Set[ℝ]) {
    flip(flip(s)) = s
} by {
    flip(s).contains = flip_fn(s.contains)
    flip(flip(s)).contains = flip_fn(flip_fn(s.contains))
    flip(flip(s)).contains = s.contains
}

/// If x is a lower bound of s, then -x is an upper bound of flip(s).
theorem lb_imp_flip_ub(s: Set[ℝ], x: ℝ) {
    x.is_set_lower_bound(s) implies (-x).is_set_upper_bound(flip(s))
} by {
    forall(y: ℝ) {
        if y ∈ flip(s) {
            -y ∈ s
            x <= -y
            -x >= y
        }
    }
}

/// If -a ≤ b, then -b ≤ a.
theorem neg_lte(a: ℝ, b: ℝ) {
    -a <= b implies -b <= a
} 

/// If x is an upper bound of s, then -x is a lower bound of flip(s).
theorem ub_imp_flip_lb(s: Set[ℝ], x: ℝ) {
    x.is_set_upper_bound(s) implies (-x).is_set_lower_bound(flip(s))
} by {
    forall(y: ℝ) {
        if y ∈ flip(s) {
            -y ∈ s
            -y <= x
            -x <= y
        }
    }
}

/// If x is the greatest lower bound of s, then -x is the least upper bound of flip(s).
theorem glb_imp_flip_lub(s: Set[ℝ], x: ℝ) {
    x.is_set_greatest_lower_bound(s)
    implies (-x).is_set_least_upper_bound(flip(s))
} by {
    x.is_set_lower_bound(s)
    (-x).is_set_upper_bound(flip(s))

    forall(y: ℝ) {
        if y.is_set_upper_bound(flip(s)) {
            (-y).is_set_lower_bound(flip(flip(s)))
            (-y).is_set_lower_bound(s)
            -y <= x
            -x <= y
        }
    }
    (-x).is_set_least_upper_bound(flip(s))
}

/// If x is the least upper bound of s, then -x is the greatest lower bound of flip(s).
theorem lub_imp_flip_glb(s: Set[ℝ], x: ℝ) {
    x.is_set_least_upper_bound(s)
    implies (-x).is_set_greatest_lower_bound(flip(s))
} by {
    x.is_set_upper_bound(s)
    (-x).is_set_lower_bound(flip(s))

    forall(y: ℝ) {
        if y.is_set_lower_bound(flip(s)) {
            (-y).is_set_upper_bound(flip(flip(s)))
            (-y).is_set_upper_bound(s)
            x <= -y
            y <= -x
        }
    }
}

/// Negating a nonempty set yields another nonempty set.
theorem flip_preserve_nonempty(s: Set[ℝ]) {
    (s).is_nonempty implies (flip(s)).is_nonempty
} by {
    let x: ℝ satisfy { x ∈ s }
    -x ∈ flip(s)
}

define have_lower_bound(s: Set[ℝ]) -> Bool {
    exists(lb: ℝ) {
        lb.is_set_lower_bound(s)
    }
}

/// The infimum (greatest lower bound) of s, if it exists.
/// Returns 0 if s is empty or has no lower bound.
let inf = function(s: Set[ℝ]) {
    if (s).is_nonempty and have_lower_bound(s) {
        -ℝ.new(sup_cut(flip(s)))
    } else {
        0
    }
}

/// True if s is nonempty and has a lower bound (so inf(s) exists).
define have_inf(s: Set[ℝ]) -> Bool {
    (s).is_nonempty and have_lower_bound(s)
}

/// If s is nonempty and has a lower bound, then inf(s) is its greatest lower bound.
/// This proves the existence of the infimum for bounded nonempty sets.
theorem lb_imp_glb(s: Set[ℝ]) {
    have_inf(s)
    implies inf(s).is_set_greatest_lower_bound(s)
} by {
    (s).is_nonempty
    have_lower_bound(s)
    let x: ℝ satisfy { x.is_set_lower_bound(s) }
    (flip(s)).is_nonempty
    (-x).is_set_upper_bound(flip(s))
    let y: ℝ satisfy {
        y.is_set_least_upper_bound(flip(s))
    }
    (flip(s)).is_nonempty
    have_upper_bound(flip(s))
    y = sup(flip(s))
    y = ℝ.new(sup_cut(flip(s)))
    -y = inf(s)
    (-y).is_set_greatest_lower_bound(flip(flip(s)))
    flip(flip(s)) = s
    (-y).is_set_greatest_lower_bound(s)
}

/// Any lower bound of a nonempty bounded set is less than or equal to its infimum.
theorem low_bound_gte_inf(s: Set[ℝ], u: ℝ) {
    (s).is_nonempty and u.is_set_lower_bound(s) implies inf(s) >= u
}

/// Any element of s is greater than or equal to its greatest lower bound.
theorem gte_inf(s: Set[ℝ], y: ℝ, a: ℝ) {
    y.is_set_greatest_lower_bound(s) and a ∈ s implies a >= y
}

/// The infimum of a set is less than or equal to its supremum.
theorem inf_lte_sup(s: Set[ℝ]) {
    have_inf(s) and have_sup(s) implies inf(s) <= sup(s)
} by {
    let x: ℝ satisfy {
        x ∈ s
    }
    inf(s) <= x
    x <= sup(s)
}

/// For any ε > 0, there exists an element of s within ε of the greatest lower bound y,
/// and this element is less than y + ε.
theorem closely_gt_inf(s: Set[ℝ], y: ℝ, ε: ℝ) {
    y.is_set_greatest_lower_bound(s) and ε > 0 implies exists(a: ℝ) {
        a ∈ s and y <= a and a < y + ε 
    }
} by {
    (-y).is_set_least_upper_bound(flip(s))
    let b: ℝ satisfy {
        b ∈ flip(s) and -y - ε < b and b <= -y
    }
    -b ∈ s
    y <= -b
    -b < y + ε
    let a = -b
    a ∈ s and y <= a and a < y + ε 
}

/// For any ε > 0, there exists an element of s within ε of inf(s),
/// and this element is less than inf(s) + ε.
theorem closely_gt_inf_directly(s: Set[ℝ], ε: ℝ) {
    have_inf(s) and ε > 0 implies exists(a: ℝ) {
        a ∈ s and inf(s) <= a and a < inf(s) + ε 
    }
} by {
    inf(s).is_set_greatest_lower_bound(s)
}

/// If u is greater than the greatest lower bound y of s, then there exists an element of s
/// between y and u.
theorem directly_closely_gt_inf(s: Set[ℝ], y: ℝ, u: ℝ) {
    y.is_set_greatest_lower_bound(s) and u > y implies exists(a: ℝ) {
        a ∈ s and y <= a and a < u 
    }
} by {
    let ε = u - y
    ε > 0
    let (a: ℝ) satisfy {
        a ∈ s and y <= a and a < y + ε 
    }
    y + ε = u
    a < u
}

/// If u is greater than inf(s), then there exists an element of s between inf(s) and u.
theorem directly_closely_gt_inf_directly(s: Set[ℝ], u: ℝ) {
    have_inf(s) and u > inf(s) implies exists(a: ℝ) {
        a ∈ s and inf(s) <= a and a < u 
    }
}

/// The sequence f is within ε of a for all indices beyond n (tail bound).
define tail_bound(f: ℕ -> ℝ, a: ℝ, n: ℕ, ε: ℝ) -> Bool {
    forall(m: ℕ) {
        m > n implies (f(m) - a).abs < ε
    }
}

/// The sequence f converges to a if for every ε > 0, f is eventually within ε of a.
define converge_to(f: ℕ -> ℝ, a: ℝ) -> Bool {
    forall(ε: ℝ) {
        ε > 0 implies exists(n: ℕ) {
            tail_bound(f, a, n, ε)
        }
    }
}

/// The sequence f converges if there exists some limit a.
define converge(f: ℕ -> ℝ) -> Bool {
    exists(a: ℝ) {
        converge_to(f, a)
    }
}

/// Returns the limit of f if it converges, otherwise returns 0.
let limit(f: ℕ -> ℝ) -> a: ℝ satisfy {
    if converge(f) {
        converge_to(f, a)
    } else {
        a = 0
    }
}

/// The sequence f is monotonically increasing.
define is_increase(f: ℕ -> ℝ) -> Bool {
    forall(n: ℕ, m: ℕ) {
        n < m implies f(n) <= f(m)
    }
}

/// The sequence f is monotonically decreasing.
define is_decrease(f: ℕ -> ℝ) -> Bool {
    forall(n: ℕ, m: ℕ) {
        n < m implies f(n) >= f(m)
    }
}

/// The sequence f increases at each successor step.
define is_suc_increase(f: ℕ -> ℝ) -> Bool {
    forall(n: ℕ) {
        f(n) <= f(n.suc)
    }
}

/// If f increases at each successor step, then f is monotonically increasing.
theorem suc_increase_imp_increase(f: ℕ -> ℝ) {
    is_suc_increase(f) implies is_increase(f)
} by {
    define p(m: ℕ) -> Bool {
        forall(n: ℕ) {
            n < m implies f(n) <= f(m)
        }
    }
    p(ℕ.0)
    forall(m: ℕ) {
        f(m) <= f(m.suc)
        if p(m) {
            forall(n: ℕ) {
                if n < m.suc {
                    n <= m
                    if n = m {
                        f(n) <= f(m)
                        f(n) <= f(m.suc)
                    } else {
                        n < m
                        f(n) <= f(m)
                        f(n) <= f(m.suc)
                    }
                    f(n) <= f(m.suc)
                }
            }
            p(m.suc)
        }
    }
}

/// The sequence f decreases at each successor step.
define is_suc_decrease(f: ℕ -> ℝ) -> Bool {
    forall(n: ℕ) {
        f(n) >= f(n.suc)
    }
}

/// If f decreases at each successor step, then f is monotonically decreasing.
theorem suc_decrease_imp_decrease(f: ℕ -> ℝ) {
    is_suc_decrease(f) implies is_decrease(f)
} by {
    define p(m: ℕ) -> Bool {
        forall(n: ℕ) {
            n < m implies f(n) >= f(m)
        }
    }
    p(ℕ.0)
    forall(m: ℕ) {
        f(m) >= f(m.suc)
        if p(m) {
            forall(n: ℕ) {
                if n < m.suc {
                    n <= m
                    if n = m {
                        f(n) >= f(m)
                        f(n) >= f(m.suc)
                    } else {
                        n < m
                        f(n) >= f(m)
                        f(n) >= f(m.suc)
                    }
                    f(n) >= f(m.suc)
                }
            }
            p(m.suc)
        }
    }
}

attributes ℝ {
    /// True if this real number is an upper bound for the sequence f.
    define is_seq_upper_bound(self, f: ℕ -> ℝ) -> Bool {
        forall(n: ℕ) {
            f(n) <= self
        }
    }

    /// True if this real number is a lower bound for the sequence f.
    define is_seq_lower_bound(self, f: ℕ -> ℝ) -> Bool {
        forall(n: ℕ) {
            self <= f(n)
        }
    }
}

/// True if the sequence f has an upper bound.
define have_seq_upper_bound(f: ℕ -> ℝ) -> Bool {
    exists(a: ℝ) {
        a.is_seq_upper_bound(f)
    }
}

/// True if the sequence f has a lower bound.
define have_seq_lower_bound(f: ℕ -> ℝ) -> Bool {
    exists(a: ℝ) {
        a.is_seq_lower_bound(f)
    }
}

/// If a sequence has an upper bound, then its image set has an upper bound.
theorem seq_upper_bound_to_upper_bound(f: ℕ -> ℝ) {
    have_seq_upper_bound(f) implies have_upper_bound(fn_image(f))
} by {
    let a: ℝ satisfy { 
        a.is_seq_upper_bound(f) 
    }
    forall(x: ℝ) {
        if x ∈ fn_image(f) {
            let n: ℕ satisfy { x = f(n) }
            f(n) <= a
            x <= a
        }
    }
    a.is_set_upper_bound(fn_image(f))
    have_upper_bound(fn_image(f))
}

/// If the image set of a sequence has an upper bound, then the sequence has an upper bound.
theorem upper_bound_to_seq_upper_bound(f: ℕ -> ℝ) {
    have_upper_bound(fn_image(f)) implies have_seq_upper_bound(f)
} by {
    let a: ℝ satisfy { 
        a.is_set_upper_bound(fn_image(f)) 
    }
    forall(n: ℕ) {
        f(n) ∈ fn_image(f) 
        f(n) <= a
    }
    a.is_seq_upper_bound(f)
    have_seq_upper_bound(f)
}

/// If a sequence has a lower bound, then its image set has a lower bound.
theorem seq_lower_bound_to_lower_bound(f: ℕ -> ℝ) {
    have_seq_lower_bound(f) implies have_lower_bound(fn_image(f))
} by {
    let a: ℝ satisfy { 
        a.is_seq_lower_bound(f) 
    }
    forall(x: ℝ) {
        if x ∈ fn_image(f) {
            let n: ℕ satisfy { x = f(n) }
            f(n) >= a
            x >= a
        }
    }
    a.is_set_lower_bound(fn_image(f))
    have_lower_bound(fn_image(f))
}

/// If the image set of a sequence has a lower bound, then the sequence has a lower bound.
theorem lower_bound_to_seq_lower_bound(f: ℕ -> ℝ) {
    have_lower_bound(fn_image(f)) implies have_seq_lower_bound(f)
} by {
    let a: ℝ satisfy { 
        a.is_set_lower_bound(fn_image(f)) 
    }
    forall(n: ℕ) {
        f(n) ∈ fn_image(f) 
        f(n) >= a
    }
    a.is_seq_lower_bound(f)
    have_seq_lower_bound(f)
}

/// The image set of any sequence is nonempty.
theorem seq_nonempty(f: ℕ -> ℝ) {
    (fn_image(f)).is_nonempty
} by {
    f(ℕ.0) ∈ fn_image(f)
}

/// Monotone convergence theorem: a bounded increasing sequence converges to its supremum.
theorem bounded_increase_convergence_to(f: ℕ -> ℝ) {
    is_increase(f) and have_seq_upper_bound(f) implies converge_to(f, sup(fn_image(f)))
} by {
    (fn_image(f)).is_nonempty
    have_upper_bound(fn_image(f))
    let a = sup(fn_image(f))
    a.is_set_least_upper_bound(fn_image(f))
    forall(ε: ℝ) {
        if ε > 0 {
            let x: ℝ satisfy {
                x ∈ fn_image(f) and a - ε < x and x <= a
            }
            let n: ℕ satisfy { x = f(n) }
            forall(m: ℕ) {
                if m > n {
                    f(m) >= f(n)
                    a - ε < f(m)
                    a.is_set_upper_bound(fn_image(f))
                    f(m) ∈ fn_image(f)
                    f(m) <= a 
                    - ε < f(m) - a
                    f(m) - a <= 0
                    f(m) - a < ε
                    (f(m) - a).abs < ε
                }
            }
            tail_bound(f, a, n, ε)
        }
    }
    converge_to(f, a)
}

/// A bounded increasing sequence converges.
theorem bounded_increase_convergence(f: ℕ -> ℝ) {
    is_increase(f) and have_seq_upper_bound(f) implies converge(f)
}

/// A monotonically increasing sequence whose image has an upper bound converges to its supremum.
theorem bounded_increase_convergence_to_ver_set(f: ℕ -> ℝ) {
    is_increase(f) and have_upper_bound(fn_image(f)) implies converge_to(f, sup(fn_image(f)))
}

/// A monotonically increasing sequence whose image has an upper bound converges.
theorem bounded_increase_convergence_ver_set(f: ℕ -> ℝ) {
    is_increase(f) and have_upper_bound(fn_image(f)) implies converge(f)
}

/// Bounded decreasing sequence converges to its infimum.
theorem bounded_decrease_convergence_to(f: ℕ -> ℝ) {
    is_decrease(f) and have_seq_lower_bound(f) implies converge_to(f, inf(fn_image(f)))
} by {
    (fn_image(f)).is_nonempty
    have_lower_bound(fn_image(f))
    let a = inf(fn_image(f))
    a.is_set_greatest_lower_bound(fn_image(f))
    forall(ε: ℝ) {
        if ε > 0 {
            let x: ℝ satisfy {
                x ∈ fn_image(f) and a <= x and x < a + ε
            }
            let n: ℕ satisfy { x = f(n) }
            forall(m: ℕ) {
                if m > n {
                    f(m) <= f(n)
                    f(m) < a + ε
                    a.is_set_lower_bound(fn_image(f))
                    f(m) ∈ fn_image(f)
                    a <= f(m)
                    f(m) - a < ε
                    0 <= f(m) - a
                    (f(m) - a).abs < ε
                }
            }
            tail_bound(f, a, n, ε)
        }
    }
    converge_to(f, a)
}

/// A bounded decreasing sequence converges.
theorem bounded_decrease_convergence(f: ℕ -> ℝ) {
    is_decrease(f) and have_seq_lower_bound(f) implies converge(f)
}

/// A monotonically decreasing sequence whose image has a lower bound converges to its infimum.
theorem bounded_decrease_convergence_to_ver_set(f: ℕ -> ℝ) {
    is_decrease(f) and have_lower_bound(fn_image(f)) implies converge_to(f, inf(fn_image(f)))
} 

/// A monotonically decreasing sequence whose image has a lower bound converges.
theorem bounded_decrease_convergence_ver_set(f: ℕ -> ℝ) {
    is_decrease(f) and have_lower_bound(fn_image(f)) implies converge(f)
}

/// If a is a subset of b and both have infima, then inf(a) ≥ inf(b).
theorem real_set_subset_to_inf_gte(a: Set[ℝ], b: Set[ℝ]) {
    have_inf(a) and have_inf(b) and a ⊆ b implies inf(a) >= inf(b)
} by {
    forall(x: ℝ) {
        if x ∈ a {
            x ∈ b
            x >= inf(b)
        }
    }
    inf(b).is_set_lower_bound(a)
    inf(a) >= inf(b)
}

/// If a is a subset of b and both have suprema, then sup(a) ≤ sup(b).
theorem real_set_subset_to_sup_lte(a: Set[ℝ], b: Set[ℝ]) {
    have_sup(a) and have_sup(b) and a ⊆ b implies sup(a) <= sup(b)
} by {
    forall(x: ℝ) {
        if x ∈ a {
            x ∈ b
            x <= sup(b)
        }
    }
    sup(b).is_set_upper_bound(a)
    sup(a) <= sup(b)
}

/// If a is a subset of b and u is an upper bound of b, then u is an upper bound of a.
theorem subset_preserve_set_upper_bound(a: Set[ℝ], b: Set[ℝ], u: ℝ) {
    a ⊆ b and u.is_set_upper_bound(b) implies u.is_set_upper_bound(a)
} by {
    forall(x: ℝ) {
        if x ∈ a {
            x ∈ b
            x <= u
        }
    }
}

/// If a is a subset of b and b has an upper bound, then a has an upper bound.
theorem subset_preserve_have_upper_bound(a: Set[ℝ], b: Set[ℝ]) {
    a ⊆ b and have_upper_bound(b) implies have_upper_bound(a)
}

/// If a is a subset of b and l is a lower bound of b, then l is a lower bound of a.
theorem subset_preserve_set_lower_bound(a: Set[ℝ], b: Set[ℝ], l: ℝ) {
    a ⊆ b and l.is_set_lower_bound(b) implies l.is_set_lower_bound(a)
} by {
    forall(x: ℝ) {
        if x ∈ a {
            x ∈ b
            l <= x
        }
    }
}

/// If a is a subset of b and b has a lower bound, then a has a lower bound.
theorem subset_preserve_have_lower_bound(a: Set[ℝ], b: Set[ℝ]) {
    a ⊆ b and have_lower_bound(b) implies have_lower_bound(a)
}