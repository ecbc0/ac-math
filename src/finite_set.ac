from list import List

/// A constant function that always returns false, regardless of input.
define const_false[T](x: T) -> Bool {
    false
}

/// True if a boolean function represents a finite set.
/// A function satisfies the finite constraint if there exists a finite list containing all elements for which the function returns true.
define finite_constraint[T](contains: T -> Bool) -> Bool {
    exists(superset: List[T]) {
        forall(x: T) {
            contains(x) implies superset.contains(x)
        }
    }
}

theorem const_false_satisfies_finite_constraint[T] {
    finite_constraint(const_false[T])
}

theorem list_contains_satisfies_finite_constraint[T](ts: List[T]) {
    finite_constraint(ts.contains)
}

/// Creates a new function that returns true for the given item and delegates to the original function for all other inputs.  
/// i.e. add a true value to f: T -> Bool by change f(item) to true 
define functional_insert[T](f: T -> Bool, item: T, x: T) -> Bool {
    if x = item {
        true
    } else {
        f(x)
    }
}

theorem functional_insert_preserve_finite_constraint[T](f: T -> Bool, item: T) {
    finite_constraint(f) implies finite_constraint(functional_insert(f, item))
}

/// Creates a new function that returns false for the given item and delegates to the original function for all other inputs.  
/// i.e. add a false value to f: T -> Bool by change f(item) to false 
define functional_remove[T](f: T -> Bool, item: T, x: T) -> Bool {
    if x = item {
        false
    } else {
        f(x)
    }
}

theorem functional_remove_preserve_finite_constraint[T](f: T -> Bool, item: T) {
    finite_constraint(f) implies finite_constraint(functional_remove(f, item))
}

/// A finite set represented as a boolean membership function.
/// The constraint ensures that only finitely many elements are members.
structure Finite_Set[T] {
    /// Finite sets are defined by a membership function.
    contains: T -> Bool
} constraint {
    finite_constraint(contains)
}

attributes Finite_Set[T] {
    /// Converts a list to a finite set.
    let from_list: List[T] -> Finite_Set[T] = function(ts: List[T]) {
        Finite_Set.new(ts.contains)
    }

    /// True if the set contains no elements.
    define is_empty(self) -> Bool {
        forall(x: T) {
            not x ∈ self
        }
    }

    /// Removes an element from the set. If the item isn't present, this is a no-op.
    define remove(self, item: T) -> Finite_Set[T] {
        Finite_Set.new(functional_remove(self.contains, item))
    }

    /// Adds an element to the set. If the item is already present, this is a no-op.
    define insert(self, item: T) -> Finite_Set[T] {
        Finite_Set.new(functional_insert(self.contains, item))
    }
}

theorem insert_contains[T](set: Finite_Set[T], item: T) {
    item ∈ set.insert(item)
}

theorem remove_does_not_contain[T](set: Finite_Set[T], item: T) {
    not item ∈ set.remove(item)
}

theorem insert_other_still_contains[T](set: Finite_Set[T], item: T, other: T) {
    item ∈ set implies item ∈ set.insert(other)
} by {
    finite_constraint(set.contains)
    finite_constraint(set.insert(other).contains)
}

theorem remove_other_still_contains[T](set: Finite_Set[T], item: T, other: T) {
    item != other and item ∈ set implies item ∈ set.remove(other)
} by {
    finite_constraint(set.contains)
    finite_constraint(set.remove(other).contains)
}

theorem insert_other_contains_imp_contains[T](set: Finite_Set[T], item: T, other: T) {
    item != other and item ∈ set.insert(other) implies item ∈ set
} by {
    finite_constraint(set.contains)
    finite_constraint(set.insert(other).contains)
}

theorem remove_other_contains_imp_contains[T](set: Finite_Set[T], item: T, other: T) {
    item != other and item ∈ set.remove(other) implies item ∈ set
} by {
    finite_constraint(set.contains)
    finite_constraint(set.remove(other).contains)
}

theorem insert_other_contains_eq[T](set: Finite_Set[T], item: T, other: T) {
    item != other implies item ∈ set = item ∈ set.insert(other)
} by {
    if item ∈ set {
        item ∈ set iff item ∈ set.insert(other)
    } else {
        not item ∈ set
        not item ∈ set.insert(other)
        item ∈ set = item ∈ set.insert(other)
    }
}

theorem remove_other_contains_eq[T](set: Finite_Set[T], item: T, other: T) {
    item != other implies item ∈ set = item ∈ set.remove(other)
} by {
    if item ∈ set {
        item ∈ set iff item ∈ set.remove(other)
    } else {
        not item ∈ set
        not item ∈ set.remove(other)
        item ∈ set = item ∈ set.remove(other)
    }
}

theorem remove_then_insert[T](set: Finite_Set[T], item: T) {
    item ∈ set implies set.remove(item).insert(item) = set
} by {
    if set.remove(item).insert(item) != set {
        set.remove(item).insert(item).contains != set.contains
        let t: T satisfy {
            t ∈ set.remove(item).insert(item) != t ∈ set
        }
        if item != t {
            if t ∈ set {
                t ∈ set.remove(item)
                false
            } else {
                false
            }
        } else {
            item = t
            not t ∈ set
            false
        }
    }
}

theorem insert_then_remove[T](set: Finite_Set[T], item: T) {
    not item ∈ set implies set.insert(item).remove(item) = set
} by {
    if set.insert(item).remove(item) != set {
        set.insert(item).remove(item).contains != set.contains
        let t: T satisfy {
            t ∈ set.insert(item).remove(item) != t ∈ set
        }
        if item != t {
            if t ∈ set {
                false
            } else {
                t ∈ set.insert(item)
                false
            }
        } else {
            item = t
            not t ∈ set
            false
        }
    }
}